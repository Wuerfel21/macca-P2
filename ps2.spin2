{
    PS/2 Keyboard Driver
    Copyright (c) 2021-22 by Marco Maccaferri <macca@maccasoft.com>

    TERMS OF USE: MIT License
}
CON

    _CLKFREQ = 160_000_000

    PS2_DATA_PIN  = 47
    PS2_CLOCK_PIN = 46

DAT             ' PS/2 Keyboard river
                org     $000

ps2_driver
                asmclk

                fltl    #PS2_DATA_PIN
                fltl    #PS2_CLOCK_PIN

                mov     ps2_data, #$F3      ' auto-repeat
                call    #ps2_transmit
                and     ps2_data, #%011_11111 ' slow 1s / 2cps
                call    #ps2_transmit

                mov     ps2_state, #%0_000  ' turn off all leds
                jmp     #ps2_locks

ps2_reset

ps2_loop        and     ps2_state, #$07     ' keep locks state

.l1             call    #ps2_receive
                getbyte ps2_code, ps2_data, #0

                cmp     ps2_data, #$F0  wz  ' release
        if_z    bith    ps2_state, #7
        if_z    jmp     #.l1

                cmp     ps2_data, #$E0  wz
        if_z    bith    ps2_state, #28
        if_z    jmp     #ps2_ext0
                cmp     ps2_data, #$E1  wz
        if_z    bith    ps2_state, #29
        if_z    jmp     #ps2_ext1

ps2_ext_ret     testbn  ps2_state, #29  wz
        if_nz   jmp     #ps2_loop           ' E1 codes are ignored

                testb   ps2_state, #7   wc  ' release

                cmp     ps2_code, #$14  wz  ' left control
        if_z    testbn  ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #LEFT_CTRL

                cmp     ps2_code, #$14  wz  ' right control
        if_z    testb   ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #RIGHT_CTRL

                cmp     ps2_code, #$12  wz  ' left shift
        if_z    testbn  ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #LEFT_SHIFT
                cmp     ps2_data, #$59  wz  ' right shift
        if_z    testbn  ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #RIGHT_SHIFT

                cmp     ps2_code, #$11  wz  ' left alt
        if_z    testbn  ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #LEFT_ALT

                cmp     ps2_code, #$11  wz  ' right alt
        if_z    testb   ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #RIGHT_ALT

                cmp     ps2_code, #$1F  wz  ' left gui
        if_z    testb   ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #LEFT_GUI
                cmp     ps2_code, #$27  wz  ' right gui
        if_z    testb   ps2_state, #28  andz
        if_z    muxnc   ps2_shift, #RIGHT_GUI

                cmp     ps2_code, #$77  wz  ' num. lock
  if_z_and_c    jmp     #ps2_loop           ' | ignore if released
        if_z    bitnot  ps2_state, #0       ' | toggle state
        if_z    jmp     #ps2_locks          ' | update leds
                cmp     ps2_code, #$58  wz  ' caps lock
  if_z_and_c    jmp     #ps2_loop           ' | ignore if released
        if_z    bitnot  ps2_state, #1       ' | toggle state
        if_z    jmp     #ps2_locks          ' | update leds
                cmp     ps2_code, #$7E  wz  ' scroll lock
  if_z_and_c    jmp     #ps2_loop           ' | ignore if released
        if_z    bitnot  ps2_state, #2       ' | toggle state
        if_z    jmp     #ps2_locks          ' | update leds

                testb  ps2_state, #28  wc   ' extended code
                rcl     ps2_code, #1        '   to bit 0

                altgb   ps2_code, #ps2_table ' get usb scancode
                getbyte ps2_key              ' |
                shl     ps2_key, #8     wz
        if_z    jmp     #ps2_loop

                call    #ps2_translate

                setbyte ps2_key, ps2_shift, #2
                setbyte ps2_key, ps2_state, #3
                debug(uhex_long(ps2_key))

                ' TODO

                jmp     #ps2_loop

ps2_locks       mov     ps2_data, #$ED
                call    #ps2_transmit
                mov     ps2_data, #0
                testb   ps2_state, #0   wz  ' num. lock
                bitz    ps2_data, #1
                testb   ps2_state, #1   wz  ' caps lock
                bitz    ps2_data, #2
                testb   ps2_state, #2   wz  ' scroll lock
                bitz    ps2_data, #0
                call    #ps2_transmit
                jmp     #ps2_loop

ps2_ext1        call    #ps2_receive
                setbyte ps2_code, ps2_data, #1
                cmp     ps2_data, #$F0  wz  ' release
        if_z    bith    ps2_state, #7
        if_z    jmp     #ps2_ext1

                ' fall through

ps2_ext0        call    #ps2_receive
                setbyte ps2_code, ps2_data, #0
                cmp     ps2_data, #$F0  wz  ' release
        if_z    bith    ps2_state, #7
        if_z    jmp     #ps2_ext0

                jmp     #ps2_ext_ret

ps2_transmit    drvl    #PS2_CLOCK_PIN      ' pull clock low
                getct   ps2_tout            ' hold clock for 128us (must be > 100us)
                addct1  ps2_tout, ps2_us128 ' |
                jnct1   #$
                drvl    #PS2_DATA_PIN       ' pull data low
                getct   ps2_tout            ' hold data for 4us
                addct1  ps2_tout, ps2_us4   ' |
                jnct1   #$

                fltl    #PS2_CLOCK_PIN      ' release clock
                getct   ps2_tout            ' allow pin to float
                addct1  ps2_tout, ps2_us1   ' |
                jnct1   #$

                test    ps2_data, #$FF  wc  ' append parity
                muxnc   ps2_data, #$100     ' |
                bith    ps2_data, #9        ' append stop bit

                getct   ps2_tout            ' safety timeout
                addct1  ps2_tout, ps2_us2000

                mov     ps2_bits, #10
.l1             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock low
        if_nz   jct1    #ps2_reset          ' | check timeout
        if_nz   jmp     #.l1                ' |
                shr     ps2_data, #1    wc  ' output data bit
                drvc    #PS2_DATA_PIN       ' |
.l2             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock high
        if_z    jct1    #ps2_reset          ' | check timeout
        if_z    jmp     #.l2                ' |
                djnz    ps2_bits, #.l1      ' another bit ?

                fltl    #PS2_DATA_PIN

.l3             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock and data low
        if_z    testpn  #PS2_DATA_PIN   wz  ' |
        if_nz   jct1    #ps2_reset          ' | check timeout
        if_nz   jmp     #.l3                ' |

.l4             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock and data high
        if_nz   testpn  #PS2_DATA_PIN   wz  ' |
        if_z    jct1    #ps2_reset          ' | check timeout
        if_z    jmp     #.l4                ' |

                ' Fall through to receive ack

ps2_receive     testpn  #PS2_CLOCK_PIN  wz  ' wait initial clock low
        if_nz   jmp     #ps2_receive        ' |

                getct   ps2_tout            ' safety timeout
                addct1  ps2_tout, ps2_us2000

                mov     ps2_bits, #11
.l1             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock low
        if_nz   jct1    #ps2_reset          ' | check timeout
        if_nz   jmp     #.l1                ' |
                testp   #PS2_DATA_PIN   wc  ' sample data
                rcr     ps2_data, #1        ' |
.l2             testpn  #PS2_CLOCK_PIN  wz  ' wait until clock high
        if_z    jct1    #ps2_reset          ' | check timeout
        if_z    jmp     #.l2                ' |
                djnz    ps2_bits, #.l1      ' another bit?

                shr     ps2_data, #22       ' align byte
                test    ps2_data, #$1FF wc  ' test parity
        if_nc   jmp     #ps2_reset          ' |
        _ret_   and     ps2_data, #$FF      ' ok


ps2_translate

                mov     ps2_temp, #ps2_map
.l1             alts    ps2_temp
                mov     ps2_data, 0-0       wz
        if_z    ret
                add     ps2_temp, #1
                getbyte ps2_code, ps2_data, #3  ' target shift state
                cmp     ps2_code, ps2_shift wz  ' compare shift state
        if_ne   jmp     #.l1                    ' skip if not equal

                setbyte ps2_data, #0, #3        ' mask map address
                getbyte ps2_code, ps2_key, #1
                cmp     ps2_code, #$80      wc  ' max. 128 keys
        if_nc   ret
                add     ps2_data, ps2_code
                rdbyte  ps2_data, ps2_data      ' read key

                testbn  ps2_state, #0       wz  ' num. lock
 if_z           cmp     ps2_code, #$54      wc
 if_z_and_nc    cmpr    ps2_code, #$62 +1   wc
 if_z_and_nc    setbyte ps2_data, #0, #0

                testbn  ps2_state, #1       wz  ' caps lock
 if_nz          cmp     ps2_data, #$61      wc
 if_nz_and_nc   cmpr    ps2_data, #$7A +1   wc
 if_nz_and_nc   xor     ps2_data, #%00100000

        _ret_   setbyte ps2_key, ps2_data, #0

ps2_us1         long    _CLKFREQ / 1000_000 * 1     ' 1 usec.
ps2_us4         long    _CLKFREQ / 1000_000 * 4     ' 4 usec.
ps2_us128       long    _CLKFREQ / 1000_000 * 128   ' 128 usec.
ps2_us2000      long    _CLKFREQ / 1000_000 * 4000  ' 2000 usec.

ps2_bits        long    0
ps2_code        long    0
ps2_state       long    0
ps2_shift       long    0
ps2_key         long    0

ps2_data        long    0
ps2_tout        long    0
ps2_temp        long    0
ps2_ptr         long    0

ps2_map
                'long                                   @map_us
                'long    LEFT_SHIFT             << 24 | @map_us_shift
                'long    RIGHT_SHIFT            << 24 | @map_us_shift
                long                                   @map_it
                long    LEFT_SHIFT             << 24 | @map_it_shift
                long    RIGHT_SHIFT            << 24 | @map_it_shift
                long    RIGHT_ALT              << 24 | @map_it_altgr
                long   (RIGHT_SHIFT|RIGHT_ALT) << 24 | @map_it_shift_altgr
                long   (LEFT_SHIFT|RIGHT_ALT)  << 24 | @map_it_shift_altgr
                long    0 ' end of maps

ps2_table       byte    $00, $00    '00
                byte    $42, $00    '01     F9
                byte    $00, $00    '02
                byte    $3E, $00    '03     F5
                byte    $3C, $00    '04     F3
                byte    $3A, $00    '05     F1
                byte    $3B, $00    '06     F2
                byte    $45, $00    '07     F12
                byte    $00, $00    '08
                byte    $43, $00    '09     F10
                byte    $41, $00    '0A     F8
                byte    $3F, $00    '0B     F6
                byte    $3D, $00    '0C     F4
                byte    $2B, $00    '0D     Tab
                byte    $35, $00    '0E     `
                byte    $00, $00    '0F
                byte    $00, $00    '10
                byte    $E2, $E6    '11     Alt-L    Alt-R
                byte    $E1, $00    '12     Shift-L
                byte    $00, $00    '13
                byte    $E0, $E4    '14     Ctrl-L   Ctrl-R
                byte    $14, $00    '15     q
                byte    $1E, $00    '16     1
                byte    $00, $00    '17
                byte    $00, $00    '18
                byte    $00, $00    '19
                byte    $1D, $00    '1A     z
                byte    $16, $00    '1B     s
                byte    $04, $00    '1C     a
                byte    $1A, $00    '1D     w
                byte    $1F, $00    '1E     2
                byte    $00, $E3    '1F              Win-L
                byte    $00, $00    '20
                byte    $06, $00    '21     c
                byte    $1B, $00    '22     x
                byte    $07, $00    '23     d
                byte    $08, $00    '24     e
                byte    $21, $00    '25     4
                byte    $20, $00    '26     3
                byte    $00, $E7    '27              Win-R
                byte    $00, $00    '28
                byte    $2C, $00    '29     Space
                byte    $19, $00    '2A     v
                byte    $09, $00    '2B     f
                byte    $17, $00    '2C     t
                byte    $15, $00    '2D     r
                byte    $22, $00    '2E     5
                byte    $00, $65    '2F              Apps
                byte    $00, $00    '30
                byte    $11, $00    '31     n
                byte    $05, $00    '32     b
                byte    $0B, $00    '33     h
                byte    $0A, $00    '34     g
                byte    $1C, $00    '35     y
                byte    $23, $00    '36     6
                byte    $00, $00    '37              Power
                byte    $00, $00    '38
                byte    $00, $00    '39
                byte    $10, $00    '3A     m
                byte    $0D, $00    '3B     j
                byte    $18, $00    '3C     u
                byte    $24, $00    '3D     7
                byte    $25, $00    '3E     8
                byte    $00, $00    '3F              Sleep
                byte    $00, $00    '40
                byte    $36, $00    '41     ,
                byte    $0E, $00    '42     k
                byte    $0C, $00    '43     i
                byte    $12, $00    '44     o
                byte    $27, $00    '45     0
                byte    $26, $00    '46     9
                byte    $00, $00    '47
                byte    $00, $00    '48
                byte    $37, $00    '49     .
                byte    $38, $54    '4A     /        (/)
                byte    $0F, $00    '4B     l
                byte    $33, $00    '4C     ;
                byte    $13, $00    '4D     p
                byte    $2D, $00    '4E     -
                byte    $00, $00    '4F
                byte    $00, $00    '50
                byte    $00, $00    '51
                byte    $34, $00    '52     '
                byte    $00, $00    '53
                byte    $2F, $00    '54     [
                byte    $2E, $00    '55     =
                byte    $00, $00    '56
                byte    $00, $00    '57
                byte    $39, $00    '58     CapsLock
                byte    $E5, $00    '59     Shift-R
                byte    $28, $58    '5A     Enter    (Enter)
                byte    $30, $00    '5B     ]
                byte    $00, $00    '5C
                byte    $31, $00    '5D     \
                byte    $00, $00    '5E              WakeUp
                byte    $00, $00    '5F
                byte    $00, $00    '60
                byte    $64, $00    '61
                byte    $00, $00    '62
                byte    $00, $00    '63
                byte    $00, $00    '64
                byte    $00, $00    '65
                byte    $2A, $00    '66     BackSpace
                byte    $00, $00    '67
                byte    $00, $00    '68
                byte    $59, $4D    '69     (1)      End
                byte    $00, $00    '6A
                byte    $5C, $50    '6B     (4)      Left
                byte    $5F, $4A    '6C     (7)      Home
                byte    $00, $00    '6D
                byte    $00, $00    '6E
                byte    $00, $00    '6F
                byte    $62, $49    '70     (0)      Insert
                byte    $63, $4C    '71     (.)      Delete
                byte    $5A, $51    '72     (2)      Down
                byte    $5D, $00    '73     (5)
                byte    $5E, $4F    '74     (6)      Right
                byte    $60, $52    '75     (8)      Up
                byte    $29, $00    '76     Esc
                byte    $53, $00    '77     NumLock
                byte    $44, $00    '78     F11
                byte    $57, $00    '79     (+)
                byte    $5B, $4E    '7A     (3)      PageDn
                byte    $56, $00    '7B     (-)
                byte    $55, $46    '7C     (*)      PrScr
                byte    $61, $4B    '7D     (9)      PageUp
                byte    $47, $00    '7E     ScrLock
                byte    $00, $00    '7F
                byte    $00, $00    '80
                byte    $00, $00    '81
                byte    $00, $00    '82
                byte    $40, $00    '83     F7

                fit     $1F0

CON

    LEFT_CTRL   = %0000_0001
    LEFT_SHIFT  = %0000_0010
    LEFT_ALT    = %0000_0100
    LEFT_GUI    = %0000_1000
    RIGHT_CTRL  = %0001_0000
    RIGHT_SHIFT = %0010_0000
    RIGHT_ALT   = %0100_0000
    RIGHT_GUI   = %1000_0000

DAT ' US
                orgh

map_us          byte    $00, $00, $00, $00, "a", "b", "c", "d"  ' 00-07
                byte    "e", "f", "g", "h", "i", "j", "k", "l"  ' 08-0F
                byte    "m", "n", "o", "p", "q", "r", "s", "t"  ' 10-17
                byte    "u", "v", "w", "x", "y", "z", "1", "2"  ' 18-1F
                byte    "3", "4", "5", "6", "7", "8", "9", "0"  ' 20-27
                byte    $0D, $1B, $08, $09, " ", "-", "=", "["  ' 28-2F
                byte    "]", "\", "#", ";", "'", $60, ",", "."  ' 30-37
                byte    "/", $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, "/", "+", "-", "+"  ' 50-57
                byte    $0D, "1", "2", "3", "4", "5", "6", "7"  ' 58-5F
                byte    "8", "9", "0", $00, "\", $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

map_us_shift    byte    $00, $00, $00, $00, "A", "B", "C", "D"  ' 00-07
                byte    "E", "F", "G", "H", "I", "J", "K", "L"  ' 08-0F
                byte    "M", "N", "O", "P", "Q", "R", "S", "T"  ' 10-17
                byte    "U", "V", "W", "X", "Y", "Z", "!", "@"  ' 18-1F
                byte    "#", "$", "%", "^", "&", "*", "(", ")"  ' 20-27
                byte    $0D, $1B, $08, $09, " ", "_", "+", "{"  ' 28-2F
                byte    "}", "|", "~", ":", $22, "~", "<", ">"  ' 30-37
                byte    "?", $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 50-57
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 58-5F
                byte    $00, $00, $00, $00, "|", $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

DAT ' Italian
                orgh

map_it          byte    $00, $00, $00, $00, "a", "b", "c", "d"  ' 00-07
                byte    "e", "f", "g", "h", "i", "j", "k", "l"  ' 08-0F
                byte    "m", "n", "o", "p", "q", "r", "s", "t"  ' 10-17
                byte    "u", "v", "w", "x", "y", "z", "1", "2"  ' 18-1F
                byte    "3", "4", "5", "6", "7", "8", "9", "0"  ' 20-27
                byte    $0D, $1B, $08, $09, " ", "'", $8D, $8A  ' 28-2F
                byte    "+", $A3, $97, $95, $85, "\", ",", "."  ' 30-37
                byte    "-", $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, "/", "+", "-", "+"  ' 50-57
                byte    $0D, $00, $00, $00, $00, $00, $00, $00  ' 58-5F
                byte    $00, $00, $00, $00, "<", $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

map_it_shift    byte    $00, $00, $00, $00, "A", "B", "C", "D"  ' 00-07
                byte    "E", "F", "G", "H", "I", "J", "K", "L"  ' 08-0F
                byte    "M", "N", "O", "P", "Q", "R", "S", "T"  ' 10-17
                byte    "U", "V", "W", "X", "Y", "Z", "!", $22  ' 18-1F
                byte    $9C, "$", "%", "&", "/", "(", ")", "="  ' 20-27
                byte    $0D, $1B, $08, $09, " ", "?", "^", $82  ' 28-2F
                byte    "*", $15, $15, $87, $F8, "|", ";", ":"  ' 30-37
                byte    "_", $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 50-57
                byte    $0D, $00, $00, $00, $00, $00, $00, $00  ' 58-5F
                byte    $00, $00, $00, $00, ">", $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

map_it_altgr    byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 00-07
                byte    $EE, $00, $00, $00, $00, $00, $00, $00  ' 08-0F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 10-17
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 18-1F
                byte    $00, $00, $80, $00, $00, $00, $00, $00  ' 20-27
                byte    $00, $00, $00, $00, $00, $00, $7E, "["  ' 28-2F
                byte    "]", $00, $00, "@", "#", $00, $00, $00  ' 30-37
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 50-57
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 58-5F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

map_it_shift_altgr
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 00-07
                byte    $EE, $00, $00, $00, $00, $00, $00, $00  ' 08-0F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 10-17
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 18-1F
                byte    $00, $00, $80, $00, $00, $00, $00, $00  ' 20-27
                byte    $00, $00, $00, $00, $00, $00, $00, "{"  ' 28-2F
                byte    "}", $00, $00, $00, $00, $00, $00, $00  ' 30-37
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 38-3F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 40-47
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 48-4F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 50-57
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 58-5F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 60-67
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 68-6F
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 70-77
                byte    $00, $00, $00, $00, $00, $00, $00, $00  ' 78-7F

CON ' License
{{

 TERMS OF USE: MIT License

 Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 associated documentation files (the "Software"), to deal in the Software without restriction, including
 without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
 following conditions:

 The above copyright notice and this permission notice shall be included in all copies or substantial
 portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
 LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
 WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}
